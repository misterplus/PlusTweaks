buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.4.2"
group = "plus.misterplus.plustweaks"
archivesBaseName = "plustweaks"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    coreMod = "plus.misterplus.plustweaks.core.PlusTweaksFMLLoadingPlugin"
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
    replaceIn "PlusTweaks.java"
}

repositories {
    maven { url 'http://maven.blamejared.com' }
    maven { url = "https://repo.spongepowered.org/maven" }
}

dependencies {
    compile files("libs/torcherino-7.6.jar")
    deobfCompile 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.554'
    provided('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

jar {
    from configurations.provided.asFileTree.files.collect {
        zipTree(it)
    }
    manifest.attributes([
            "FMLCorePlugin": "plus.misterplus.plustweaks.core.PlusTweaksFMLLoadingPlugin",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "FMLAT": "plustweaks_at.cfg"
    ])
}

mixin {
    add sourceSets.main, "mixins.plustweaks.refmap.json"
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar